"""Build rules for compiling protocol buffers & gRPC service stubs.
"""
subinclude(
    "///proto//build_defs/sdk",
)

USE_CUSTOM_DOC_TEMPLATE = "use_customer_doc_template"

def doc_proto_library(
        name:str,
        srcs:list,
        deps:list=[],
        visibility:list=None,
        labels:list&features&tags=[],
        test_only:bool&testonly=False,
        root_dir:str='',
        protoc_flags:list=[],
        additional_context:dict=None,
):
    """Compile a .proto file to generate code for golang.

    Args:
      name (str): Name of the rule
      srcs (list): Input .proto files.
      deps (list): Dependencies (other grpc_library or proto_library rules)
      visibility (list): Visibility specification for the rule.
      labels (list): List of labels to apply to this rule.
      test_only (bool): If True, this rule can only be used by test rules.
      root_dir (str): The directory that the protos are compiled relative to. Useful if your
                      proto files have import statements that are not relative to the repo root.
      protoc_flags (list): Additional flags to pass to protoc.
      additional_context (dict): Additional context.
    """
    html_template = "///core//build_defs/proto:protoc-gen-doc-template.html"
    if additional_context and USE_CUSTOM_DOC_TEMPLATE in additional_context:
        html_template = additional_context[USE_CUSTOM_DOC_TEMPLATE]

    tools = {"protoc_gen_doc": [CONFIG.CORE.PROTOC_GEN_DOC]}
    plugin_flags = [
        '--doc_out="$OUT_DIR"',
        '--plugin=protoc-gen-doc="$(which $TOOLS_PROTOC_GEN_DOC)"',
        f'--doc_opt=$TOOLS_TEMPLATE,{name}.html,source_relative',
    ]
    tools["template"]= html_template

    protoc = protoc_rule(
        name = name,
        srcs = srcs,
        language = "doc",
        tools = tools,
        protoc_flags = protoc_flags,
        plugin_flags = plugin_flags,
        labels = labels,
        test_only = test_only,
        root_dir = root_dir,
        deps = deps,
        visibility = visibility,
    )

    return filegroup(
        name = f"_{name}#doc",
        srcs = [protoc],
        test_only = test_only,
        labels = labels,
        exported_deps = deps,
        visibility = visibility,
    )


def doc_proto_language():
    return proto_language(
        language = "doc",
        build_def = doc_proto_library,
    )
