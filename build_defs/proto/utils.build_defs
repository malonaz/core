subinclude(
    "//build_defs:remote",
    "///proto//build_defs:proto",
)

def github_proto_library(
        name:str,
        repo:str,
        revision:str,
        dir:str,
        files:list,
        deps:list=[],
        root_dir:str=None,
        visibility:list=None,
        protoc_flags:list=[],
        additional_context:dict=None,
):
    """
    A github proto library.
    """
    src = github_proto_srcs(
        name = f"_{name}#srcs",
        dir = dir,
        files = files,
        repo = repo,
        revision = revision,
    )

    root_dir_sanitized = "third_party/proto"
    protoc_flag = "-I" + package_name()
    if root_dir:
        root_dir_sanitized += f"/{root_dir}"
        protoc_flag += f"/{root_dir}"

    return proto_library(
        name = name,
        srcs = [src],
        root_dir=root_dir_sanitized,
        protoc_flags = protoc_flags + [protoc_flag],
        additional_context = additional_context,
        deps = deps,
        visibility=visibility,
    )

def buf_validate_proto_library(
        name:str = "validate",
        version:str = "v1.0.0",
        visibility:list = None,
):
    """Buf protovalidate library."""
    return github_proto_library(
        name = name,
        additional_context = {
            GO_IMPORT_PATH: "buf.build/gen/go/bufbuild/protovalidate/protocolbuffers/go/buf/validate",
        },
        dir = "proto/protovalidate",
        files = ["buf/validate/validate.proto"],
        repo = "bufbuild/protovalidate",
        revision = version,
        root_dir = "proto/protovalidate",
        visibility = visibility,
    )

def googleapis_proto_library(
        name:str,
        dir:str,
        files:list,
        revision:str,
        deps:list = [],
        go_import_suffix:str = None,
        visibility:list = None,
):
    """Google APIs proto library."""
    if go_import_suffix is None:
        go_import_suffix = dir.replace("/", "/")
    return github_proto_library(
        name = name,
        additional_context = {
            GO_IMPORT_PATH: f"google.golang.org/genproto/googleapis/{go_import_suffix}",
        },
        dir = dir,
        files = files,
        repo = "googleapis/googleapis",
        revision = revision,
        deps = deps,
        visibility = visibility,
    )

def malonaz_proto_library(
        name:str,
        dir:str,
        files:list,
        deps:list = [],
        go_import_suffix:str = None,
        revision:str,
        visibility:list = None,
):
    """Malonaz proto library."""
    if go_import_suffix is None:
        go_import_suffix = dir.replace("/", "__")
    return github_proto_library(
        name = name,
        additional_context = {
            GO_IMPORT_PATH: f"github.com/malonaz/core/genproto/{go_import_suffix}",
        },
        dir = dir,
        files = files,
        repo = "malonaz/core",
        revision = revision,
        deps = deps,
        visibility = visibility,
    )
