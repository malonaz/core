subinclude(
    "//build_defs:remote",
    "///proto//build_defs:proto",
)

def github_proto_library(
        name:str,
        repo:str,
        revision:str,
        dir:str,
        files:list,
        deps:list=[],
        root_dir:str=None,
        visibility:list=None,
        protoc_flags:list=[],
        additional_context:dict=None,
):
    """
    A github proto library.
    """
    src = github_proto_srcs(
        name = f"_{name}#srcs",
        dir = dir,
        files = files,
        repo = repo,
        revision = revision,
    )

    root_dir_sanitized = "third_party/proto"
    protoc_flag = "-I" + package_name()
    if root_dir:
        root_dir_sanitized += f"/{root_dir}"
        protoc_flag += f"/{root_dir}"

    return proto_library(
        name = name,
        srcs = [src],
        root_dir=root_dir_sanitized,
        protoc_flags = protoc_flags + [protoc_flag],
        additional_context = additional_context,
        deps = deps,
        visibility=visibility,
    )

def external_github_proto_library(
        name:str,
        repo:str,
        revision:str,
        files:list,
        go_dep:str,
        dir:str=None,
        visibility:list=None,
):
    """ Used to grab third party protofiles, but use an external go_dep.
    This is used when we don't want to compile the go library ourselves and want to use an external one instead.
    """
    srcs = []
    for f in files:
        sanitized_filename = f.replace("/", "_")
        src = github_file(
            name = f"_{name}#{sanitized_filename}",
            out = f,
            file = f,
            repo = repo,
            revision = revision,
        )
        if dir is None:
            srcs += [src]
            continue
        new_src_name = f"_{name}#{sanitized_filename}_correct_packages"
        new_src = genrule(
            name = new_src_name,
            srcs = [src],
            out = join_path(dir, basename(f)),
            cmd = "cat $SRCS > $OUT",
            visibility=visibility,
        )
        srcs += [new_src]
    return filegroup(
        name = name,
        srcs = srcs,
        exported_deps = srcs,
        visibility=visibility,
        provides = {
            "go": go_dep,
            "proto": srcs,
        },
    )
