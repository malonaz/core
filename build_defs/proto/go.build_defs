"""Build rules for compiling protocol buffers & gRPC service stubs.
"""
subinclude(
    "///proto//build_defs/sdk",
    "///go//build_defs:go",
)

GO_IMPORT_PATH = "go_import_path"
GENERATE_GO_GRPC = "generate_go_grpc"
GENERATE_GO_GRPC_GATEWAY = "generate_go_grpc_gateway"
GENERATE_GO_COBRA_CLI = "generate_go_cobra_cli"
GENERATE_GO_AIP = "generate_go_aip"
GENERATE_GO_LLM = "generate_go_llm"
GENERATE_GO_RPC = "generate_go_rpc"
GENERATE_GO_MODEL = "generate_go_model"
GENERATE_GO_POSTGRES = "generate_go_postgres"

def go_path_mapping(plugin_out_keys:list):
    """Used to update the Go path mapping; by default it doesn't really import in the way we want."""
    def _map_go_paths(rule_name):
        labels = get_labels(rule_name, 'proto:go-map:')
        mapping = ''
        if len(labels) > 0:
            mapping = 'M' + ',M'.join(labels) + ':'

        new_cmd = get_command(rule_name)
        for plugin_out_key in plugin_out_keys:
            new_cmd = new_cmd.replace(f'--{plugin_out_key}=', f'--{plugin_out_key}={mapping}')
        set_command(rule_name, new_cmd)
    return _map_go_paths


def go_mapping_labels(name:str, srcs:list, root_dir:str):
    base_path = get_base_path()
    diff_pkg = basename(base_path) != name
    if CONFIG.GO.IMPORT_PATH:
        base_path = join_path(CONFIG.GO.IMPORT_PATH, base_path)
    destination = f"{base_path}/{name}" if diff_pkg else base_path
    return [f'proto:go-map: {base_path}/{src}={destination}' for src in srcs
               if not looks_like_build_label(src)]

def _import_path(name, root_dir, additional_context=None):
    pkg = package_name()
    if basename(pkg) != name:
        pkg += "/" + name
    if root_dir:
        pkg = pkg.removeprefix(root_dir + "/")
    if additional_context and GO_IMPORT_PATH in additional_context:
        return additional_context[GO_IMPORT_PATH]
    if CONFIG.GO.IMPORT_PATH:
        return join_path(CONFIG.GO.IMPORT_PATH, pkg)
    return pkg


def go_proto_library(
        name:str,
        srcs:list,
        deps:list=[],
        visibility:list=None,
        labels:list&features&tags=[],
        test_only:bool&testonly=False,
        root_dir:str='',
        protoc_flags:list=[],
        additional_context:dict=None,
):
    """Compile a .proto file to generate code for golang.

    Args:
      name (str): Name of the rule
      srcs (list): Input .proto files.
      deps (list): Dependencies (other grpc_library or proto_library rules)
      visibility (list): Visibility specification for the rule.
      labels (list): List of labels to apply to this rule.
      test_only (bool): If True, this rule can only be used by test rules.
      root_dir (str): The directory that the protos are compiled relative to. Useful if your
                      proto files have import statements that are not relative to the repo root.
      protoc_flags (list): Additional flags to pass to protoc.
      additional_context (dict): Additional context.
    """
    tools= {"protoc_gen_go": [CONFIG.CORE.PROTOC_GEN_GO]}
    deps += [CONFIG.CORE.PROTOC_GEN_GO_DEP]
    plugin_out_keys = ['go_out']
    plugin_flags = [
        '--plugin=protoc-gen-go="$(which $TOOLS_PROTOC_GEN_GO)"',
        '--go_out="$OUT_DIR"',
        '--go_opt=paths=source_relative',
    ]

    # PROTOC-GEN-GRPC
    if additional_context and GENERATE_GO_GRPC in additional_context:
        tools["protoc_gen_grpc_go"] = CONFIG.CORE.PROTOC_GEN_GRPC_GO
        deps += [CONFIG.CORE.PROTOC_GEN_GRPC_GO_DEP]
        plugin_out_keys += ['go-grpc_out']
        plugin_flags += [
            '--plugin=protoc-gen-go-grpc="$(which $TOOLS_PROTOC_GEN_GRPC_GO)"',
            '--go-grpc_out="$OUT_DIR"',
            '--go-grpc_opt=paths=source_relative,require_unimplemented_servers=false',
        ]

    # PROTOC-GEN-COBRA
    if additional_context and GENERATE_GO_COBRA_CLI in additional_context:
        tools['protoc_gen_cobra_go'] = [CONFIG.CORE.PROTOC_GEN_COBRA_GO]
        deps += [CONFIG.CORE.PROTOC_GEN_COBRA_GO_DEP]
        plugin_out_keys += ['cobra_out']
        plugin_flags += [
            '--plugin=protoc-gen-cobra="`which $TOOLS_PROTOC_GEN_COBRA_GO`"',
            '--cobra_out=$OUT_DIR',
            '--cobra_opt=paths=source_relative',
        ]

    # PROTOC-GEN-GRPC-GATEWAY
    if additional_context and GENERATE_GO_GRPC_GATEWAY in additional_context:
        tools['protoc_gen_grpc_gateway_go'] = [CONFIG.CORE.PROTOC_GEN_GRPC_GATEWAY_GO]
        deps += [CONFIG.CORE.PROTOC_GEN_GRPC_GATEWAY_GO_DEP]
        plugin_out_keys += ['grpc-gateway_out']
        plugin_flags += [
            '--plugin=protoc-gen-grpc-gateway="`which $TOOLS_PROTOC_GEN_GRPC_GATEWAY_GO`"',
            '--grpc-gateway_out=$OUT_DIR',
            f'--grpc-gateway_opt=paths=source_relative',
        ]

    # PROTOC-GEN-AIP
    if additional_context and GENERATE_GO_AIP in additional_context:
        tools["protoc_gen_aip_go"] = CONFIG.CORE.PROTOC_GEN_AIP_GO
        deps += [CONFIG.CORE.PROTOC_GEN_AIP_GO_DEP]
        plugin_out_keys += ['go-aip_out']
        plugin_flags += [
            '--plugin=protoc-gen-go-aip="$(which $TOOLS_PROTOC_GEN_AIP_GO)"',
            '--go-aip_out="$OUT_DIR"',
            '--go-aip_opt=paths=source_relative',
        ]

    protoc_go_templates = []

    if additional_context and GENERATE_GO_LLM in additional_context:
        protoc_go_templates += ["///core//build_defs/codegen/go_proto_templates/templates:llm"]
        deps += [CONFIG.CORE.PROTOC_GEN_LLM_GO_DEP]


    if additional_context and GENERATE_GO_RPC in additional_context:
        protoc_go_templates += ["///core//build_defs/codegen/go_proto_templates/templates:rpc"]
        tools["protoc_templates"] = [CONFIG.CORE.PROTOC_GEN_TEMPLATES_GO]
        deps += [CONFIG.CORE.PROTOC_GEN_RPC_GO_DEP]

    if additional_context and GENERATE_GO_MODEL in additional_context:
        protoc_go_templates += ["///core//build_defs/codegen/go_proto_templates/templates:model"]
        deps += [CONFIG.CORE.PROTOC_GEN_MODEL_GO_DEP]

    if additional_context and GENERATE_GO_POSTGRES in additional_context:
        protoc_go_templates += ["///core//build_defs/codegen/go_proto_templates/templates:postgres"]
        deps += [CONFIG.CORE.PROTOC_GEN_POSTGRES_GO_DEP]

    if len(protoc_go_templates) > 0:
        tools["protoc_templates"] = [CONFIG.CORE.PROTOC_GEN_TEMPLATES_GO]
        deps += protoc_go_templates
        plugin_out_keys += ['templates_out']
        plugin_flags += [
            '--plugin=protoc-gen-templates="`which $TOOLS_PROTOC_TEMPLATES`"',
            '--templates_out="$OUT_DIR"',
            f'--templates_opt=paths=source_relative,' + ','.join([f"template=$(location {t})" for t in protoc_go_templates]),
        ]

    protoc = protoc_rule(
        name = name,
        srcs = srcs,
        language = "go",
        tools = tools,
        protoc_flags = protoc_flags,
        plugin_flags = plugin_flags,
        labels = labels + go_mapping_labels(name, srcs, root_dir),
        test_only = test_only,
        root_dir = root_dir,
        deps = deps,
        visibility = visibility,
        pre_build = go_path_mapping(plugin_out_keys),
    )

    return go_library(
        name = f"_{name}#go",
        srcs = [protoc],
        deps = deps,
        test_only = test_only,
        labels = labels,
        visibility = visibility,
        import_path = _import_path(name, root_dir, additional_context),
    )

def go_grpc_library(name:str, srcs:list, deps:list=[], visibility:list=None, labels:list&features&tags=[],
                      test_only:bool&testonly=False, root_dir:str='', protoc_flags:list=[], additional_context:dict=None):
    """Defines a rule for a go grpc library.

    Args:
      name (str): Name of the rule
      srcs (list): Input .proto files.
      deps (list): Dependencies (other grpc_library or proto_library rules)
      visibility (list): Visibility specification for the rule.
      labels (list): List of labels to apply to this rule.
      test_only (bool): If True, this rule can only be used by test rules.
      root_dir (str): The directory that the protos are compiled relative to. Useful if your
                      proto files have import statements that are not relative to the repo root.
      protoc_flags (list): Additional flags to pass to protoc.
      additional_context (dict): Additional context.
    """
    if additional_context is None:
        additional_context = {}
    additional_context[GENERATE_GO_GRPC] = True
    return go_proto_library(
        name = name,
        srcs = srcs,
        deps = deps,
        visibility = visibility,
        labels = labels,
        test_only=test_only,
        root_dir=root_dir,
        protoc_flags=protoc_flags,
        additional_context = additional_context,
    )

def go_grpc_gateway_library(name:str, srcs:list, deps:list=[], visibility:list=None, labels:list&features&tags=[],
                      test_only:bool&testonly=False, root_dir:str='', protoc_flags:list=[], additional_context:dict=None):
    """Defines a rule for a go grpc library.

    Args:
      name (str): Name of the rule
      srcs (list): Input .proto files.
      deps (list): Dependencies (other grpc_library or proto_library rules)
      visibility (list): Visibility specification for the rule.
      labels (list): List of labels to apply to this rule.
      test_only (bool): If True, this rule can only be used by test rules.
      root_dir (str): The directory that the protos are compiled relative to. Useful if your
                      proto files have import statements that are not relative to the repo root.
      protoc_flags (list): Additional flags to pass to protoc.
      additional_context (dict): Additional context.
    """
    if additional_context is None:
        additional_context = {}
    additional_context[GENERATE_GO_GRPC_GATEWAY] = True
    return go_grpc_library(
        name = name,
        srcs = srcs,
        deps = deps,
        visibility = visibility,
        labels = labels,
        test_only=test_only,
        root_dir=root_dir,
        protoc_flags=protoc_flags,
        additional_context = additional_context,
    )

def go_grpc_gateway_language():
    return proto_language(
        language = "go",
        build_def = go_grpc_gateway_library,
        additional_provides={"go_src": "go_srcs", "modinfo": "go_modinfo"},
    )

def go_grpc_language():
    return proto_language(
        language = "go",
        build_def = go_grpc_library,
        additional_provides={"go_src": "go_srcs", "modinfo": "go_modinfo"},
    )

def go_proto_language():
    return proto_language(
        language = "go",
        build_def = go_proto_library,
        additional_provides={"go_src": "go_srcs", "modinfo": "go_modinfo"},
    )
